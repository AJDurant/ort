// Apply core plugins.
apply plugin: 'application'

applicationName = 'pran'
mainClassName = 'com.here.provenanceanalyzer.Main'

sourceSets {
    funTest {
        kotlin.srcDirs 'src/funTest/kotlin'
        resources.srcDirs 'src/funTest/resources'
    }
}

dependencies {
    compile project(':model')

    compile 'com.beust:jcommander:1.72'
    compile 'com.vdurmont:semver4j:2.0.2'

    funTestCompile sourceSets.main.output
    funTestCompile sourceSets.test.output
    funTestCompile configurations.testCompile
    funTestRuntime configurations.testRuntime

    funTestCompile 'io.kotlintest:kotlintest:2.0.4'
}

task funTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'Verification'
    classpath = sourceSets.funTest.runtimeClasspath
    testClassesDirs = sourceSets.funTest.output.classesDirs
}

task funTestCoverage(type: JacocoReport, dependsOn: funTest) {
    description = 'Generates the code coverage report for the functional tests.'
    group = 'Reporting'

    executionData funTest
    sourceSets sourceSets.main

    reports {
        // Enable XML in addition to HTML for CI integration.
        xml.enabled true
    }
}

check.dependsOn funTest
