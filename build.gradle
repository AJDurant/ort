plugins {
    // We need to hard-code the version here because of
    // https://github.com/gradle/gradle/issues/1697
    id 'org.jetbrains.kotlin.jvm' version '1.1.2'

    id 'com.github.ben-manes.versions' version '0.14.0'
}

apply plugin: 'application'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean isNonFinalVersion = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }

            if (isNonFinalVersion) {
                selection.reject('Release candidate')
            }
        }
    }
}

mainClassName = 'ProvenanceAnalyzer'
applicationName = 'pran'
applicationDefaultJvmArgs = [
        '-Dorg.slf4j.simpleLogger.levelInBrackets=true',
        '-Dorg.slf4j.simpleLogger.showLogName=false',
        '-Dorg.slf4j.simpleLogger.showThreadName=false'
]

sourceSets {
    funTest {
        kotlin {
            srcDir 'src/funTest/kotlin'
        }

        resources {
            srcDir 'src/funTest/resources'
        }

        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    funTestCompile.extendsFrom mainCompile
    funTestRuntime.extendsFrom mainRuntime
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.2'

    compile 'com.beust:jcommander:1.72'

    // Use slf4j-simple as logger for kotlin-logging, as the latter is a wrapper for slf4j and depends on an
    // implementation of slf4j.
    compile 'io.github.microutils:kotlin-logging:1.4.4'
    compile 'org.slf4j:slf4j-simple:1.7.25'

    funTestCompile 'io.kotlintest:kotlintest:2.0.2'
}

task funTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'Verification'
    classpath = sourceSets.funTest.runtimeClasspath
    testClassesDir = sourceSets.funTest.output.classesDir
    reports.html.destination = file("${buildDir}/reports/tests/funTest")
}

check.dependsOn funTest
