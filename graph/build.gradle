// Apply third-party plugins.
plugins {
    // We need to hard-code the version here because of
    // https://github.com/gradle/gradle/issues/1697
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'

    // Note that the detekt Gradle plugin version does not necessarily
    // match the detekt tool version.
    id 'io.gitlab.arturbosch.detekt' version '1.0.0.M13.2'

    id 'com.github.ben-manes.versions' version '0.15.0'
}

// Apply core plugins.
apply plugin: 'application'
apply plugin: 'jacoco'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean isNonFinalVersion = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }

            if (isNonFinalVersion) {
                selection.reject('Release candidate')
            }
        }
    }
}

import org.apache.tools.ant.taskdefs.condition.Os

applicationName = 'graph'
mainClassName = 'com.here.provenanceanalyzer.graph.Main'
if (Os.isFamily(Os.FAMILY_UNIX)) {
    applicationDefaultJvmArgs = ['-Dsun.java2d.directx=True']
} else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    applicationDefaultJvmArgs = ['-Dsun.java2d.opengl=True']
}

repositories {
    jcenter()
}

ext {
    jacksonVersion = '2.9.0'
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.3-2'

    compile project(':model')

    compile 'com.beust:jcommander:1.72'
    compile 'org.graphstream:gs-core:1.3'
    compile 'org.graphstream:gs-ui:1.3'
    compile 'org.graphstream:gs-algo:1.3'

    // Use logback-classic as the logger for slf4k as it allows to change the log level at runtime.
    compile('ch.frankel.log4k:slf4k-api:1.0.0') {
        exclude module: 'kotlin-runtime'
    }
    compile 'ch.qos.logback:logback-classic:1.2.3'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.1'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.1'
    }
}

detekt {
    version = '1.0.0.M13.2'

    profile('main') {
        config = '../detekt.yml'
    }
}

jacoco {
    toolVersion = '0.7.9'
}

check.dependsOn detektCheck
